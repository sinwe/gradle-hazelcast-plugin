buildscript {
    apply from: file('versions.gradle')

    dependencies {
        classpath libraries.gradleRelease
        classpath libraries.owasp
        classpath libraries.version
    }
}

plugins {
    id "java-gradle-plugin"
    id "com.jfrog.artifactory" version "5.2.5"
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: 'signing'
apply plugin: "maven-publish"
apply plugin: 'net.researchgate.release'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'com.github.ben-manes.versions'

group = "com.github.sinwe.gradle.caching.hazelcast"
description = "A simple settings plugin that enables build caching in Gradle with a Hazelcast node as the backend"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        hazelcastPlugin {
            id = "com.github.sinwe.gradle.caching.hazelcast"
            implementationClass = "com.github.sinwe.gradle.caching.hazelcast.HazelcastPlugin"
        }
    }
}

def localRepo = file("$buildDir/repo")

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                name = 'Gradle Hazelcast Plugin'
                packaging = 'jar'
                description = 'A simple settings plugin that enables build caching in Gradle with a Hazelcast node as the backend'
                url = 'https://github.com/sinwe/gradle-hazelcast-plugin'

                scm {
                    connection = 'scm:git:git@github.com:sinwe/gradle-hazelcast-plugin.git'
                    developerConnection = 'scm:git:git@github.com:sinwe/gradle-hazelcast-plugin.git'
                    url = 'https://github.com/sinwe/gradle-hazelcast-plugin'
                }

                licenses {
                    license {
                        name = 'Apache License, Version 2.0, January 2004'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'sinwe'
                        name = 'Winarto'
                        email = 'winarto@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "mavenCentral"
            url = version.endsWith('SNAPSHOT') ?
                "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

repositories {
    mavenCentral()
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    implementation libraries.hazelcastClient
    testImplementation libraries.junit
    testImplementation libraries.spockCore
    testImplementation libraries.spockJunit4
    testImplementation libraries.hazelcast
    testRuntimeOnly files(createClasspathManifest)
}

// --- Configure publications ------------------------------------------

def buildTagFor(String version) {
    switch (version.substring(version.lastIndexOf("-") + 1)) {
        case "SNAPSHOT":
            return "snapshot"
        case ~/M\d+[a-z]*$/:
            return "milestone"
        default:
            return "release"
    }
}

def targetRepoKey = "libs-${buildTagFor(project.version)}s-local"

artifactory {
    contextUrl = "https://repo.gradle.org/gradle"
    publish {
        repository {
            repoKey = targetRepoKey
            username = project.findProperty("artifactory_user") ?: "nouser"
            password = project.findProperty("artifactory_password") ?: "nopass"
        }
        defaults {
            publications("maven")
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion }
    sign publishing.publications.maven
}

release {
    git {
        requireBranch.set('master')
    }
}

afterReleaseBuild.dependsOn publish
