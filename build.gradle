buildscript {
    apply from: file('versions.gradle')

    dependencies {
        classpath libraries.gradleRelease
    }
}

plugins {
    id "java-gradle-plugin"
    id "com.jfrog.artifactory" version "4.4.9"
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: 'signing'
apply plugin: "maven-publish"
apply plugin: "maven"
apply plugin: 'net.researchgate.release'

group = "com.github.sinwe.gradle.caching.hazelcast"
description = "A simple settings plugin that enables build caching in Gradle with a Hazelcast node as the backend"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

gradlePlugin {
    plugins {
        hazelcastPlugin {
            id = "com.github.sinwe.gradle.caching.hazelcast"
            implementationClass = "com.github.sinwe.gradle.caching.hazelcast.HazelcastPlugin"
        }
    }
}

def localRepo = file("$buildDir/repo")

publishing {
    publications {
        jar(MavenPublication) {
            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile libraries.hazelcastClient
    testCompile libraries.junit
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4@jar"
    testCompile libraries.hazelcast
    testRuntime files(createClasspathManifest)
}

// --- Configure publications ------------------------------------------

def buildTagFor(String version) {
    switch (version.substring(version.lastIndexOf("-") + 1)) {
        case "SNAPSHOT":
            return "snapshot"
        case ~/M\d+[a-z]*$/:
            return "milestone"
        default:
            return "release"
    }
}

def targetRepoKey = "libs-${buildTagFor(project.version)}s-local"

artifactory {
    contextUrl = "https://repo.gradle.org/gradle"
    publish {
        repository {
            repoKey = targetRepoKey
            username = project.findProperty("artifactory_user") ?: "nouser"
            password = project.findProperty("artifactory_password") ?: "nopass"
            maven = true
        }
        defaults {
            publications("jar")
        }
    }
    resolve {
        repoKey = "repo"
    }
}

ext.sonatypeUsername = hasProperty("ossrhUsername") ? ossrhUsername : System.getenv("ossrhUsername")

ext.sonatypePassword = hasProperty("ossrhPassword") ? ossrhPassword : System.getenv("ossrhPassword")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Gradle Hazelcast Plugin'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A simple settings plugin that enables build caching in Gradle with a Hazelcast node as the backend'
                url 'https://github.com/sinwe/gradle-hazelcast-plugin'

                scm {
                    connection 'scm:git:git@github.com:sinwe/gradle-hazelcast-plugin.git'
                    developerConnection 'scm:git:git@github.com:sinwe/gradle-hazelcast-plugin.git'
                    url 'https://github.com/sinwe/gradle-hazelcast-plugin'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0, January 2004'
                        url 'https://www.apache.org/licenses/LICENSE-2.0.html'
                    }
                }

                developers {
                    developer {
                        id 'sinwe'
                        name 'Winarto'
                        email 'winarto@gmail.com'
                    }
                }
            }
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion }
    sign configurations.archives
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

afterReleaseBuild.dependsOn uploadArchives
